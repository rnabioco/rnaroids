
""" deduplicate ERCCs and count """ 

# directory shortcuts
filtered = "{data}/fastq/filtered/"

rule count_erccs:
    """
    count ercc alignments 
    run on all bams at once to produce a single count table
    subread v1.4.4
    """
    input:
      bam = expand(filtered + "erccs/{sample}_ercc_dedup.bam", 
              sample = SAMPLE, data = DATA),
      ercc_gtf = ERCC_GTF
    output:
      "{data}/ercc/ercc_counts.txt" 
    params:
      job_name = "ercc_cnt",
      memory = "select[mem>12] rusage[mem=12]",
    log:
      "{data}/ercc/logs/ercc_count.log"
    threads: 12
    resources: all_threads=12
    message: "counting erccs "
    shell:
      """
      featureCounts \
        -a {input.ercc_gtf} \
        -T {threads} \
        -o {output} \
        -s 2 \
        -Q 10 \
        -p \
        -B \
        -C \
        {input.bam}
      """
 
rule dedup_ercc:
    """ 
    deduplicate ercc containing bams   
    """   
    input:
       filtered + "erccs/{sample}_ercc.bam",
    output:
       filtered + "erccs/{sample}_ercc_dedup.bam" 
    params:
      job_name = "{sample}.ercc_dedup",
      memory = "select[mem>12] rusage[mem=12]",
    log:
      "{data}/fastq/logs/ercc/dedup_{sample}.log"
    resources: all_threads=1
    message:
      "deduplicating erccs from {wildcards.sample} " 
    shell:
      """
      module load python3 #needed for load umi_tools
      # use updated v4.4
      umitools="/vol1/software/modules-python/python3/3.6.1/bin/umi_tools"
      $umitools dedup \
        --method="directional" \
        --paired \
        -v 1 \
        -L {log} \
        -I {input} \
        -S {output}
      """  

